apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    def final APP_NAME = 'TinyApps'
    def final APP_ID = 'tinyapps.apptemplate'
    def final APP_ID_DEMO = 'tinyapps.apptemplate.demo'
    def versionBuild = 1;
    def date = new Date();
    def formattedDate = date.format('yyyy_MM_dd');

    defaultConfig {
        //applicationId APP_ID
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled false
    }

    signingConfigs {
        debug {
            try {
                storeFile file('tinyappsdebug.key.jks')
                storePassword debug_storePassword
                keyAlias debug_keyAlias
                keyPassword debug_keyPassword
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define debug_storePassword, debug_keyPassword and debug_keyAlias in gradle.properties.")
            }

        }

        release {
            try {
                storeFile file('tinyappsrelease.key.jks')
                storePassword release_storePassword
                keyAlias release_keyAlias
                keyPassword release_keyPassword
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define release_storePassword, release_keyPassword and release_keyAlias in gradle.properties.")
            }
        }
    }

    productFlavors {
        Dev_Demo_ {
            applicationId APP_ID_DEMO
            manifestPlaceholders = [nameApp: APP_NAME + ' Dev Demo']
            buildConfigField 'boolean', 'IS_LOG', 'true'
            buildConfigField 'boolean', 'IS_DEMO', 'true'
            buildConfigField 'String', 'API_URL', '"http://demo.xxx.tinyapps.com"';

        }

        Dev_Live_ {
            applicationId APP_ID
            manifestPlaceholders = [nameApp: APP_NAME + ' Dev Live']
            buildConfigField 'boolean', 'IS_LOG', 'false'
            buildConfigField 'boolean', 'IS_DEMO', 'false'
            buildConfigField 'String', 'API_URL', '"http://live.xxx.tinyapps.com"';

        }

        Production_Demo_ {
            applicationId APP_ID_DEMO
            manifestPlaceholders = [nameApp: APP_NAME + ' Production Demo']
            buildConfigField 'boolean', 'IS_DEMO', 'true'
            buildConfigField 'boolean', 'IS_LOG', 'true'
            buildConfigField 'String', 'API_URL', '"http://demo.xxx.tinyapps.com"';
        }

        Production_Live_ {
            applicationId APP_ID
            manifestPlaceholders = [nameApp: APP_NAME + ' Production Live']
            buildConfigField 'boolean', 'IS_DEMO', 'false'
            buildConfigField 'boolean', 'IS_LOG', 'false'
            buildConfigField 'String', 'API_URL', '"http://live.xxx.tinyapps.com"';
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            buildConfigField 'boolean', 'DEBUG_LOGIN', 'true'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def File apk = output.outputFile
                    def String newName = "${apk.name}-v${versionBuild}-t${formattedDate}.apk"
                    output.outputFile = new File(apk.parent, newName)
                }
//                variant.outputs.each { output ->
//                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace("-debug", formattedDate + "_" + versionBuild));
//                }

            }

        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            buildConfigField 'boolean', 'DEBUG_LOGIN', 'false'
            proguardFiles 'proguard-rules-default.pro', 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def File apk = output.outputFile
                    def String newName = "${apk.name}-v${versionBuild}-t${formattedDate}.apk"
                    output.outputFile = new File(apk.parent, newName)
                }
            }
        }
    }

//    sourceSets {
//        debug.assets.srcDirs = ['src/main/assets_debug']
//        release.assets.srcDirs = ['src/main/assets']
//    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
        setHtmlReport(true)
        setHtmlOutput(new File("D:\\AndroidStudio\\Template\\BuildReport\\Lint\\report.html"))
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dexcount {
    format = "list"
    includeClasses = true
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
}

dependencies {
    compile project(':lib_anonation')
    apt project(':lib_compiler')
    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'
    compile 'com.hannesdorfmann.fragmentargs:annotation:3.0.2'
    apt 'com.hannesdorfmann.fragmentargs:processor:3.0.2'
    compile 'org.parceler:parceler-api:1.1.5'
    apt 'org.parceler:parceler:1.1.5'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.joanzapata.iconify:android-iconify-fontawesome:2.2.2'
    compile 'com.joanzapata.iconify:android-iconify-material:2.2.2'
    compile 'com.joanzapata.iconify:android-iconify-material-community:2.2.2'
    compile 'com.squareup.phrase:phrase:1.1.0'
    compile 'com.github.lsjwzh.RecyclerViewPager:lib:v1.1.0'
    compile 'com.github.lsjwzh.RecyclerViewPager:tablayoutsupport:v1.1.0'
    compile 'com.github.devlight.navigationtabbar:library:1.2.0'
    compile 'cn.pedant.sweetalert:library:1.3'
    compile 'jp.wasabeef:recyclerview-animators:2.2.3'
    //compile "com.daimajia.swipelayout:library:1.2.0@aar"
    compile 'in.srain.cube:ultra-ptr:1.0.11'

}